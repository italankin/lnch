apply plugin: 'com.android.application'

android {
    compileSdk 33
    defaultConfig {
        applicationId "com.italankin.lnch"
        minSdkVersion 23
        targetSdk 33
        versionCode 15
        versionName "1.9.3"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    signingConfigs {
        debug {
            storeFile file("debug.keystore")
            storePassword "android"
            keyAlias "android"
            keyPassword "android"
        }
        release {
            def release = file('release.properties')
            if (release.exists()) {
                Properties props = new Properties()
                props.load(new FileInputStream(release))
                storeFile file(props['keystore'])
                storePassword props['keystore_password']
                keyAlias props['alias']
                keyPassword props['alias_password']
            }
        }
    }
    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    implementation libs.moxy.api
    annotationProcessor libs.moxy.compiler

    implementation libs.dagger.api
    annotationProcessor libs.dagger.compiler
    compileOnly libs.javax.annotation

    implementation libs.rxJava
    implementation libs.rxAndroid

    implementation libs.gson

    implementation libs.timber

    implementation libs.appCompat
    implementation libs.fragment
    implementation libs.constraintLayout
    implementation libs.recyclerView
    implementation libs.scrollingPagerIndicator
    implementation libs.palette
    implementation libs.material
    implementation libs.preference
    implementation libs.customTabs
    implementation libs.annotation
    implementation libs.adapterDelegates

    implementation libs.flexbox

    lintChecks project(':lintchecks')
}
