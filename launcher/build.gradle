apply plugin: 'com.android.application'
apply from: '../gradle/versions.gradle'

android {
    compileSdkVersion rootProject.compileSdk
    defaultConfig {
        applicationId "com.italankin.lnch"
        minSdkVersion rootProject.minSdk
        targetSdkVersion rootProject.targetSdk
        versionCode 12
        versionName "1.9.0"

        buildConfigField("boolean", "SAMSUNG_ANR_FIX", "false")
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    signingConfigs {
        debug {
            storeFile file("debug.keystore")
            storePassword "android"
            keyAlias "android"
            keyPassword "android"
        }
        release {
            def release = file('release.properties')
            if (release.exists()) {
                Properties props = new Properties()
                props.load(new FileInputStream(release))
                storeFile file(props['keystore'])
                storePassword props['keystore_password']
                keyAlias props['alias']
                keyPassword props['alias_password']
            }
        }
    }
    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    implementation moxy
    annotationProcessor moxyCompiler

    implementation dagger
    annotationProcessor daggerCompiler
    compileOnly javaxAnnotation

    implementation rxJava
    implementation rxAndroid

    implementation gson

    implementation timber

    implementation appCompat
    implementation fragment
    implementation constraintLayout
    implementation recyclerView
    implementation scrollingPagerIndicator
    implementation palette
    implementation material
    implementation preference
    implementation customTabs
    implementation annotation

    implementation flexbox
    implementation picasso

    lintChecks project(':lintchecks')
}
