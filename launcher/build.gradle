apply plugin: 'com.android.application'

android {
    compileSdk 34
    namespace "com.italankin.lnch"
    defaultConfig {
        applicationId "com.italankin.lnch"
        minSdkVersion 23
        targetSdk 34
        versionCode 20
        versionName "2.0.0"
    }
    buildFeatures {
        buildConfig true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    signingConfigs {
        debug {
            storeFile file("debug.keystore")
            storePassword "android"
            keyAlias "android"
            keyPassword "android"
        }
        release {
            def release = file('release.properties')
            if (release.exists()) {
                Properties props = new Properties()
                props.load(new FileInputStream(release))
                storeFile file(props['keystore'])
                storePassword props['keystore_password']
                keyAlias props['alias']
                keyPassword props['alias_password']
            }
        }
    }
    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    implementation libs.moxy.api
    annotationProcessor libs.moxy.compiler

    implementation libs.dagger.api
    annotationProcessor libs.dagger.compiler
    compileOnly libs.javax.annotation

    implementation libs.rxJava
    implementation libs.rxAndroid

    implementation libs.gson

    implementation libs.timber

    implementation libs.androidx.appCompat
    implementation libs.androidx.fragment
    implementation libs.androidx.constraintLayout
    implementation libs.androidx.recyclerView
    implementation libs.androidx.viewPager2
    implementation libs.androidx.palette
    implementation libs.material
    implementation libs.androidx.preference
    implementation libs.androidx.customTabs
    implementation libs.androidx.annotation
    implementation libs.adapterDelegates

    implementation libs.flexbox

    lintChecks project(':lintchecks')
}
